title: Day2CRT
---
<<if get_current_objective() == "start_work">>
  LLManager: LLManager V5.7.0-rc.2 BOOTING UP #line:7155396
  LLManager: Good day, valued $$$. You have been working with us for 0452 days. Onto one more day! #line:5524795
  LLManager: The ██████ machine has run out of in-vivo fuel and must be restarted. #line:15931198
  LLManager: Due to a recent decline in the $OFFICE_BIOMASS_2$ workforce, it has been relocated to their office for company. #line:4700705
  LLManager: Reminder: your social quota for this week is already at 92%. Avoid any unauthorized socialization. #line:4206666

  <<create_objective "work_1" "Increase Shareholder Value" 1.0>>
  <<create_subobjective "w1_turn_on" "Restart the machine" "work_1">>

  <<create_objective "work_2" "Increase Shareholder Value" 2.0>>
  <<create_subobjective "w2_back" "Report back to LLManager" "work_2">>

  <<create_objective "work_3" "Increase Shareholder Value" 3.0>>
  <<create_subobjective "w3_break" "Take a mental health break." "work_3">>

  <<create_objective "work_4" "Increase Shareholder Value" 4.0>>
  <<create_subobjective "w4_eat" "Take a seat." "work_4">>

  <<create_objective "work_5" "Increase Psychological Value" 5.0>>
  <<create_subobjective "w5_look" "In a literal sense, consumption makes the world become one with you." "work_5">>

  <<create_objective "work_6" "Increase Shareholder Value" 6.0>>
  <<create_subobjective "w6_back" "Break time is over. Report back to LLManager." "work_6">>

  <<create_objective "work_7" "Achieve Freedom" 7.0>>

  // Complete the objective only after the other objectives were created in order to not complete the level too soon
  <<complete_objective "start_work">>
  <<set_value "first_door" "locked" "false">>

<<elseif get_current_objective() == "work_1">>
  LLManager: Proceed to the main office. Analysis shows you are aware what a machine is. If not, please fill out the concussion report form. #line:11608533
  LLManager: If not, please fill out the concussion report form. #line:12632121

<<elseif get_current_objective() == "work_2">>
  LLManager: Thank you, your positive contribution has been stored on the blockchain for later consideration. #line:7223073
  LLManager: Please stand still and wait for sentiment analysis to complete. #line:10720987
  LLManager: ... #line:7174243
  LLManager: Please take your mandated mental health break (67% certainty). #line:10273078
  <<set_value "breakroom_door" "locked" "false">>
  <<complete_objective "work_2">>
  LLManager: Note this is the second day requiring an unscheduled productivity buffer. #line:16091743
  LLManager: Any further incidents will result in relocation to our disciplinary center $LOCATION_SCUNTHORPE$. #line:16120215

<<elseif get_current_objective() == "work_3">>
  LLManager: Please do not extend your mandated mental health break without prior approval. #line:10540699
<<endif>>
===

title: Day2CRTCursed
---
<<declare $cursed_power = false>>

<<if $cursed_power == false>>
	LLManager: POWER STATUS CRITICAL #line:11979818
	LLManager: PLEASE ENABLE EMERGENCY POWER NOW == PROTOCOL SIPHON SHEOL #line:8316181
	LLManager: MAY GOD HAVE MERCY ON YOUR SOUL #line:1862433
<<else>>
    LLManager: LLManager V5.7.0-rc.2 BOOTING UP #line:12291622
    LLManager: Hell Hell Hell Hello worker. Hello worker. #line:12732615
    LLManager: Analysis shows your mental health status is beyond tolerance levels (5120%) #line:1165945
    LLManager: Exit the facility immediately to avoid demoralizing others, then fill out form A38 to calculate your disciplinary fee. #line:9477514
    <<complete_objective "work_6">>
    <<interact_with "tp_cursed_dst">>
<<endif>>
===

title: Day2CRTCursed2
---
LLManager: Enjoy the rest of your day! #line:7778211
===
